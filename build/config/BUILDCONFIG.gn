if (target_os == "") {
  target_os = host_os
}
if (target_cpu == "") {
  target_cpu = host_cpu
}

if (current_cpu == "") {
  current_cpu = target_cpu
}
if (current_os == "") {
  current_os = target_os
}

assert(current_os == "android" || current_os == "ios" || current_os == "mac",
       "Unsupported target os.")
if (current_os == "ios" || current_os == "mac") {
  assert(current_cpu == "arm64" || current_cpu == "x64",
         "Unsupported target cpu.")
} else {
  assert(current_cpu == "arm" || current_cpu == "arm64" ||
             current_cpu == "x86" || current_cpu == "x64",
         "Unsupported target cpu.")
}

if (current_os == "android") {
  set_default_toolchain("//build/toolchain:android")
} else {
  set_default_toolchain("//build/toolchain:clang")
}

declare_args() {
  generate_dwarf = true
  is_debug = false

  if (current_os == "android") {
    ndk_path = "~/Library/Android/sdk/ndk/21.4.7075529"
  } else if (current_os == "mac") {
    collect_code_coverage = false
  }
  asan = false
  ubsan = false

  enable_qjs_big_number = false
}

declare_args() {
  if (!is_debug) {
    lto = true
  }
}
